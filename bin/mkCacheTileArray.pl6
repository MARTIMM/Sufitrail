#!/usr/bin/env perl6

use v6;
use SufiTrail::Tracks;

#-------------------------------------------------------------------------------
sub MAIN (
  Str:D $gpx-file where $gpx-file.IO ~~ :r,
  Int :$min-zoom = 10, Int :$max-zoom = 18
) {

  note "\nGetting tile coordinates from gpx data in $gpx-file";

  my Hash $h = get-tile-coords( $gpx-file, $min-zoom, $max-zoom);
  generate-sufi-cache( $h, $min-zoom, $max-zoom);
}

#-------------------------------------------------------------------------------
sub get-tile-coords (
  Str:D $gpx-file, Int $min-zoom, Int $max-zoom
  --> Hash
) {

  my Hash $h = {};

  my SufiTrail::Tracks $t .= new(:$gpx-file);
  for $t.get-gpx -> [ $long, $lat] {

    for $min-zoom ..^ $max-zoom -> $z {

      my Int $x = $t.long2tile( $long, $z);
      my Int $y = $t.lat2tile( $lat, $z);

#      note "ln, lt: $long, $lat --> $x, $y, $z";
      $h{$z} = {} unless $h{$z}:exists;
      for $x-1,$x,$x+1 -> $x0 {
        $h{$z}{$x} = {} unless $h{$z}{$x}:exists;
        for $y-1,$y,$y+1 -> $y0 {
          $h{$z}{$x}{$y} = 1;
        }
      }
    }
  }

  $h
}

#-------------------------------------------------------------------------------
sub generate-sufi-cache ( Hash $h, Int $min-zoom, Int $max-zoom ) {

  my Str $tile-info-text = '';
  my Int $tile-count = 0;
  for $min-zoom ..^ $max-zoom -> $z {
    $tile-info-text ~= "\n    $z: \{";
    for $h{$z}.keys.sort -> $x {
      $tile-info-text ~= "\n      $x: [\n        ";

      for $h{$z}{$x}.keys.sort -> $y {
        $tile-info-text ~= " $y,";
        $tile-count++;
      }

      $tile-info-text .= chop;
      $tile-info-text ~= "\n      ],";
    }

    $tile-info-text .= chop;
    $tile-info-text ~= "\n    },";
  }

  $tile-info-text .= chop;

  note "$tile-count tiles needed to cache";

  my Str $program-path = 'Data/js-libs/SufiTrail/SufiCacheData.js';
  $program-path.IO.spurt(Q:s:to/EOSCRIPT/);
    /* Author: Marcel Timmerman
       License: ...
       Copyright: Â© Sufitrail 2017, 2018

       This module is generated by bin/mkCacheTileArray.pl6. Please do not
       modify because it will be overwritten in subsequent generations.
    */

    "use strict";

    goog.provide('SufiTrail.SufiCacheData');

    /** ============================================================================
      Object to hold data to aid in caching
      @constructor
    */
    SufiTrail.SufiCacheData = function ( ) {
      // Adaptor/mediator
      this.center = null;
      this.tileCacheInfo = null;
    }

    /** ----------------------------------------------------------------------------
      Initializes the tile coordinate structure to aid in caching tiles
      @public
      @param {SufiTrail.SufiCenter} center SufiTrail core object
    */
    SufiTrail.SufiCacheData.prototype.init = function ( center ) {

      this.center = center;

      // Tile coordinate data for zoomlevels from $min-zoom to $max-zoom
      // There are $tile-count tile coordinates
      this.tileCacheInfo = {$tile-info-text
      }
    }

    EOSCRIPT
}
